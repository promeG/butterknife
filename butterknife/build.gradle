import java.text.SimpleDateFormat

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
  }

  buildTypes {
    release {
      consumerProguardFiles 'proguard-rules.txt'
    }
  }
}

dependencies {
  compile project(':butterknife-annotations')
  compile deps.supportAnnotations
  provided deps.supportv4

  testCompile deps.junit
  testCompile deps.truth
  testCompile deps.robolectric
}

version = VERSION_NAME
group = GROUP

install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging 'aar'
        // Add your description here
        name POM_DESCRIPTION
        url POM_URL
        // Set your license
        licenses {
          license {
            name POM_LICENCE_NAME
            url POM_LICENCE_URL
          }
        }
        developers {
          developer {
            id POM_DEVELOPER_ID
            name POM_DEVELOPER_NAME
            email POM_DEVELOPER_EMAIL
          }
        }
        scm {
          connection GIT_URL
          developerConnection GIT_URL
          url POM_URL
        }
      }
    }
  }
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.source
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.source
}

artifacts {
  archives androidJavadocsJar
  archives androidSourcesJar
}

bintray {
  user = bintray_user
  key = bintray_api_key
  configurations = ['archives']
  dryRun = false
  publish = true
  pkg {
    repo = 'maven'
    name = 'butterknife'
    websiteUrl = POM_URL
    issueTrackerUrl = POM_URL + '/issues'
    vcsUrl = GIT_URL
    licenses = ['Apache-2.0']
    publicDownloadNumbers = true
    version {
      name = VERSION_NAME
      released  = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date()) //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
      vcsTag = VERSION_NAME
    }
  }
}
