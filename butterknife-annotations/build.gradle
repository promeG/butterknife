import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def logger = new com.android.build.gradle.internal.LoggerWrapper(project.logger)
def sdkHandler = new com.android.build.gradle.internal.SdkHandler(project, logger)
for (File file : sdkHandler.sdkLoader.repositories) {
  repositories.maven {
    url = file.toURI()
  }
}

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

checkstyle {
  configFile rootProject.file('checkstyle.xml')
  showViolations true
}

configurations {
  provided
  compile.extendsFrom provided
}

dependencies {
  provided deps.android
  compile deps.supportAnnotations
}

version = VERSION_NAME
group = GROUP

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}


publishing {
  publications {
    mavenJava(MavenPublication) {
      if (plugins.hasPlugin('war')) {
        from components.web
      } else {
        from components.java
      }

      artifact sourcesJar
      artifact javadocJar
    }
  }
}

bintray {
  user = bintray_user
  key = bintray_api_key

  publications = ['mavenJava']
  dryRun = false
  publish = true
  pkg {
    repo = 'maven'
    name = 'butterknife-annotations'
    desc = POM_DESCRIPTION
    websiteUrl = POM_URL
    vcsUrl = GIT_URL
    licenses = ['Apache-2.0']
    publicDownloadNumbers = true
    version {
      name = VERSION_NAME //Bintray logical version name
      released  = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date()) //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
      vcsTag = VERSION_NAME
    }
  }
}
